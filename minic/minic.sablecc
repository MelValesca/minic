Language minic;
Lexer
d = '0'..'9';
w = 'a'..'z';
int = d+;
id = w(d|w)*;
blank = (#9 | #10 | #14 | #32)+;
comment = ('//'|'#') (Any - #10)* | Shortest '/*' Any* '*/';
Ignored blank, comment;

Parser
prog = funs ;
funs = {many:} funs fun |
       {none:} ;

fun = type id '(' params ')' block ;

params = {many:} params ',' param |
         {one:} param |
         {none:} ;

param = type id;

block = '{' stmts '}' ;
stmts = {some:} stmts stmt |
        {none:} ;
stmt = {var:} type id '=' exp ';' |
       {assign:} id '=' exp ';' |
       {tabvar:} id '[' int ']' '=' exp ';' |
       {ptrassign:} '*' id '=' exp ';' |
       {memvar:} typeptr id '=' 'new' type ';' |
       {tabassign:} typeptr id '=' 'new' type '[' int ']' ';' |
       {if:} 'if' '(' exp ')' block |
       {ifelse:} 'if' '(' exp ')' block 'else' [else:]block |
       {while:} 'while' '(' exp ')' block |
       {printint:} 'printint' '(' exp ')' ';' |
       {printbool:} 'printbool' '(' exp ')' ';' |
       {deletetab:} 'delete' '[]' id ';' |
       {delete:} 'delete' id ';' |
       {println:} 'println' '(' ')' ';' |
       {call:} id '(' args ')' ';' |
       {return:} 'return' exp ';' ;

args = {many:} args ',' exp |
       {one:} exp |
       {none:};

type = {int:} 'int' |
       {bool:} 'bool' |
       typeptr ;

typeptr = {ptrbool:} 'bool' '*' |
       {ptrint:} 'int' '*' ;


exp = {add:} [left:]exp '+' [right:]exp |
      {sub:} [left:]exp '-' [right:]exp |
      {mul:} [left:]exp '*' [right:]exp |
      {and:} [left:]exp '&&' [right:]exp |
      {or:} [left:]exp '||' [right:]exp |
      {not:} '!' exp |
      {int:} int |
      {true:} 'true' |
      {false:} 'false' |
      {par:} '(' exp ')' |
      {addr:} '&' id |
      {lt:} [left:]exp '<' [right:]exp |
      {var:} id |
      {tabvar:} id '[' int ']' |
      {ptr:} '*' id |
      {call:} id '(' args ')';

   Priority
      Left not;
      Left mul;
      Left add, sub;
      Left lt;
      Left and;
      Left or;
